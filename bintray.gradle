apply plugin: 'com.jfrog.bintray'

apply plugin: "com.github.dcendents.android-maven"

version = "0.4.0"
group = "pokercc.android.ExpandableRecyclerView"
def projectName = "ExpandableRecyclerView"
def username="pokercc"
def _email="pokercc@sina.com"

def siteUrl = 'https://github.com/pokercc/ExpandableRecyclerView'    // project homepage
def gitUrl = 'https://github.com/pokercc/ExpandableRecyclerView.git'
// project git
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name projectName
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id username
                        name username
                        email _email
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }

            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives sourcesJar
}

/** Task: bintrayUpload */
def localProperties() {
    Properties properties = new Properties()
    File localFile = project.rootProject.file('local.properties')
    if (localFile.exists()) {
        properties.load(localFile.newDataInputStream())
    }
    return properties
}

Properties properties = localProperties()



bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = 'android'
        name = projectName
        websiteUrl = siteUrl
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        publish = true
    }
}